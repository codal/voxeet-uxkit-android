buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        classpath 'com.jaredsburrows:gradle-license-plugin:0.8.70'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.2'
    }
}

def modules_sdk = [
        'uxkit', 'uxkit-exoplayer-support', 'uxkit-firebase',
        'uxkit-incoming-call', 'uxkit-self-managed-call', 'uxkit-system-service',
        'uxkit-youtube'
]

ext {
    buildToolsVersion = "29.0.3"
    minSdkVersion = 21
    compileSdkVersion = 29
    targetSdkVersion = 29
    supportLibVersion = "28.0.0"

    //underlying sdk
    //voxeetSdkVersion defined in the sdk.gradle file
    //voxeetUXKitVersion defined in the uxkit.gradle file
}

apply from: "./uxkit.gradle"
apply from: "./sdk.gradle"

allprojects {
    repositories {
        maven { url "https://android-sdk.voxeet.com/beta" }
        maven { url "https://android-sdk.voxeet.com/release" }
        mavenLocal()
        google()
        jcenter()
    }
}


task cleanAll {
    dependsOn = modules_sdk.collect { ":${it}:clean" }
}

task assembleAll {
    dependsOn = modules_sdk.collect { ":${it}:assembleRelease" }
}

task licenseAll {
    dependsOn = modules_sdk.collect { ":${it}:licenseReleaseReport" }

    modules_sdk.collect { "${it}/src/main/assets" }
            .each { folder -> new File(folder).mkdirs() }

    modules_sdk.each { name ->
        def report = "${name}/build/reports/licenses/licenseReleaseReport.json"
        def new_report = "${name}/build/reports/licenses/${name}.json"
        new File(report).renameTo(new File(new_report))

        copy { from new_report into "${name}/src/main/assets/" }
    }
}

task installAll {
    dependsOn = modules_sdk.collect { ":${it}:install" }
}

task uploadAll {
    dependsOn = modules_sdk.collect { ":${it}:bintrayUpload" }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
}
